// FAUST standard library
import("stdfaust.lib");
lsrev = library("lsrev.lib");

// LUCA-SPANEDDA-DIGITAL-REVERBS-LIBRARY ---------------------------------------
// lsrev.lib = library ---- lsrev.function for recall a function ---------------


// UTILITIES--------------------------------------------------------------------
//
//------------------------------------------------------------------------------
// CONVERSION MILLISECONDS to SAMPLES
//------------------------------------------------------------------------------
// (t) = give time in milliseconds we want to know in samples
Mstosamps(t) = (ma.SR/1000)*t : int;
//
//
//------------------------------------------------------------------------------
// CONVERSION SAMPLES to MILLISECONDS
//------------------------------------------------------------------------------
// (samps) = give tot. samples we want to know in milliseconds
Sampstoms(samps) = ((1000/ma.SR)*samps) : int;
//
//
//------------------------------------------------------------------------------
// T60 DECAY TIME from SAMPLES
//------------------------------------------------------------------------------
// (samps,seconds) = give: samples of the filter, seconds we want for t60 decay
T60samps(samps,seconds) = 1/(10^((3*(((1000 / ma.SR)*samps)/1000))/seconds));
//
//
//------------------------------------------------------------------------------
// T60 DECAY TIME from MILLISECONDS
//------------------------------------------------------------------------------
// (ms,seconds) = give: ms delay of the filter, seconds we want for t60 decay
T60ms(ms,seconds) = 1/(10^((3*(ms/1000))/seconds));
//
//
//------------------------------------------------------------------------------
// DIRAC IMPULSE (1 SAMPLE IMPULSE)
//------------------------------------------------------------------------------
Dirac = 1-(1:mem);
//
//
//------------------------------------------------------------------------------
// METERS TO SAMPLES (SOUND TO THE WALL AND BACK TIME)
//------------------------------------------------------------------------------
// (meters) = give a distance in meters for samples of the filter
Meterstosamps(meters) = ((ma.SR/1000.)*((1000*meters)/343.1)*2);
//
//


//-FILTERS----------------------------------------------------------------------
//
//------------------------------------------------------------------------------
// ONEZERO FILTER (FIR of I째 Order)
//------------------------------------------------------------------------------
// (g) = give amplitude 0-1(open-close) to the delayed signal 
// (g) = +1 lowpass, (g) = -1 highpass
Ozf(g) = _ <: (mem*g), _ :> +;
//
//
//------------------------------------------------------------------------------
// ONEPOLE FILTER (IIR of 1 sample delay)
//------------------------------------------------------------------------------
// (g) = give amplitude 1-0(open-close) for the lowpass cut
Opf(g) = _*g : +~ (_ : *(1- g));
// only the pole section
Op(g) = + ~ *(g);
//
//
//------------------------------------------------------------------------------
// DC BLOCKER
//------------------------------------------------------------------------------
// The dc blocker is a small recursive filter specified by the difference equation
// It is needed to remove the dc component of the signal.
// y(n) = x(n) -x(n-1)+Ry(n-1)
// R is a parameter that is typically somewhere between 0.9 and 1.0
// reference : 
// https://ccrma.stanford.edu/~jos/fp/DC_Blocker.html
DCblocker = Ozf(-1) : Op(0.998);
//
//
//------------------------------------------------------------------------------
// FEEDFORWARD COMB FILTER (FIR of N째 sample delay)
//------------------------------------------------------------------------------
// (t,g) = delay time in samples, filter gain 0-1
Ffcf(t,g) = _ <: ( _@(t-1) *g), _ :> _;
//
//
//------------------------------------------------------------------------------
// FEEDBACK COMB FILTER (IIR of N째 sample delay)
//------------------------------------------------------------------------------
// (t,g) = give: delay time in samples, feedback gain 0-1
Fbcf(t,g) = _ : (+  @(t-1)~ *(g)) : mem;
//
//
//------------------------------------------------------------------------------
// LOWPASS FEEDBACK COMB FILTER (IIR of N째 sample delay)
//------------------------------------------------------------------------------
// (t,g,cut) = give: delay samps, feedback gain 0-1, lowpass cut 1-0(open-close)
Lfbcf(t,g,cut) = (+ : @(t-1) : _*cut : +~(_ : *(1-cut)))~ *(g) : mem;
//
//
//------------------------------------------------------------------------------
// ALLPASS FILTER (FIR + IIR COMB FILTER)
//------------------------------------------------------------------------------
// (t,g) = give: delay in samples, feedback gain 0-1
Apf(t,g) = (+: _<: @(t-1), *(g))~ *(-g) : mem, _ : + : _;
//
//
//------------------------------------------------------------------------------
// ALLPASS FILTER - fixed - POSITIVE FEEDBACK
//------------------------------------------------------------------------------
// (t,g) = give: delay in samples, feedback gain 0-1
Apffp(t,g) = (+: _<: @(min(max(t-1,0),ma.SR)), *(-g))~ *(g) : mem, _ : + : _;
//
//
//------------------------------------------------------------------------------
// ALLPASS FILTER - fixed - NEGATIVE FEEDBACK
//------------------------------------------------------------------------------
// (t,g) = give: delay in samples, feedback gain 0-1
Apffn(t,g) = (+: _<: @(min(max(t-1,0),ma.SR)), *(g))~ *(-g) : mem, _ : + : _;
//
//
